%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%  Labor Optmization Model.
% Goal: create daily staffing plan for worker. 
% Reqiurement: 
%   1. Forcasting of labor hours by hour and zone. 
%   2. Pre-configured shift plan with start and end hour. Eg, full time shift
%      start from 5 to 12.
%   3. Parameters like #zones,#leadtime
%
% Output:
%   How many worker needed for each shift plan.
%
% Todo: 
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%              Define sets
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
/* 1. set of time in hour */    
%  s, t	index of hour set T, where T = {1,.,24}
set of int:  Hour = 1..24;

/* 2. temperature zone set */  
% k	index of zone set K, where K = {1,.,4} {Ambient, Deli, Frozen, Refrigerated}
set of int:  Zone = 1..4;

/* 3. schedule set */            
% Index of shift set F, {f412, f513, f614, f715, p1620â¦}
% can be 8 hours full shift, and part time shift like 6, 4 hours 
% defined as a enumerated set in .dzn file and this can be store dependdent.
enum ShiftSet;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%            Define picking parameters
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

/* 1. TS(k): Time allowed to Start picking */
% maximum hours allowed to start picking before the delivery time of 
% orders due to exposure to room temperature. eg, frozen order, 5 hours 
% [5,3,3,3]
array[Zone] of float: Window_Start;

/* 2. TE(k): Time allowed to End picking */
% minimum hour allowed to finish picking before the delivery time of
% orders due to time of a picking tour, say 2  --was not indexed by k before 
int: Window_End = 2;

%   <-----------------------Window_Start ------------------------>
%   |                                                            |
%   |                                       <--   Window_End  -->|
%   |                                       |                    |
%   .------------------------------------------------------------.
%   |                                       |                  delivery
%   <~~~~~~~  allowed picking window   ~~~~~>                  hour


/* 3. O(max): maximum allowed temporary staff for any hour period ânot used */
int: Max_Temp_Pickers = 8;

/* 4. D(tk): demand in the number of orders to be delivered at time t in zone k */ 
array[Hour, Zone] of float: Demand;

/* 5. L(f): shift length in hour for each shift f belongs to F */
array[ShiftSet] of float: Shift_Len;

/* 6. A(ft): 0-1 Matrix representation of set F */
array[ShiftSet, Hour] of int: Shift_Map;

/* 7. ratio of full time to part time */
float: Ratio_Full_to_Part = 3;

/* 8. theshhold of part time hour */
int: Hour_Part = 6;


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%            Define penalty parameters
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
/* 1. pk: the number of item units that an picker can pick in zone k per time */
% period ânot used
%array[Zone] of int: picking_num_by_zone;

/* 2. Î±: penalty coefficient on inventory per unit (order) per time period */  
float: penalty_inventory  = 0.1;

/* 3. Î²: penalty coefficient on using temporary pickers per person per time period */ 
float: penalty_temp_picker = 0.25;


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%            Define Variables 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

/* 1. P(stk): the number of regular picking hours required at time s to satisfy */
/* demand in delivery time t in zone k */
float: Max_Pick_per_Hour = 1000.0; % upbound and it's a float
array[Hour, Zone, Hour] of var 0..Max_Pick_per_Hour: PickScheduling;


/* 2. O(stk): the number of overtime picking labor hours required at time s to */
/* satisfy demand in delivery time t in zone k */ 

array[Hour, Zone, Hour] of var 0..Max_Pick_per_Hour: OverPickScheduling;

/* 3. X(f): the number of pickers assigned to shift f */  
int: Max_Picker_per_Hour = 100;
array[ShiftSet] of var 0..Max_Picker_per_Hour: Shift_Assign;


/* 4.  I(tk): the number of inventory (in orders) in zone k at in delivery time t. */ 
% not used
% The total Inventory generated by two tpyes of picking activities.
% s is the picking time and t is the deliver time.
% where i is Inventory between s and t.

% var float: Inventory[i in Hour, Zone] = 
%        sum([PickScheduling[s in Hour,k in Zone, t in Hour] + 
%		 OverPickScheduling[s in in Hour,k in Zone, t in Hour] | i in s..t]);

% array comprehension.
% forall( [a[i] != a[j] | i,j in 1..3 where i < j]) 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%            Define Objective 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Minimize: number of picker used + penalty on temporary pickers + penalty on invertory
var float: objective = 
  sum(f in ShiftSet)(Shift_Assign[f]) +
  penalty_temp_picker * sum(s in Hour, k in Zone, t in Hour)(OverPickScheduling[s,k,t]);
%   + penalty_inventory * sum(k in Zone, i in Hour)(Inventory[k,i]); % not used


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%           Define Constraints 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

/* 1. limit the available permanent picker in any hour period to the */
%  number of pickers scheduled
% â(tâ¥s+1)â(k in K) P_stk â¤  â(f in F)(A(f,t) * Xf)  for all s in T
constraint
forall(s in Hour) (
   sum(k in Zone, t in Hour where t >= s+ 1)(PickScheduling[s,k,t]) <= 
   sum(f in ShiftSet)(Shift_Map[f,s]*Shift_Assign[f])
  );


/* 2. limit the number of temperary pickers --NOT ACTIVE */
% â(t=s+1)â(k in K) O_stk â¤ O(max)    for all s in T 
constraint
forall(s in Hour) (
   sum(k in Zone, t in Hour where t >= s+ 1)(OverPickScheduling[s,k,t]) <= 
   Max_Temp_Pickers
  );
  


/* 3. Demand satisfication */
% IndexDomain: (t,k);
% Definition: sum(s | (s < t) and (s >= (t - Window_Start(k))), 
% PickScheduling(s,k,t) + OverPickScheduling(s,k,t)) >= Demand(t, k);
% /\ and 
% \/ or 
constraint 
forall(t in Hour, k in Zone) (
   sum(s in Hour where s >= (t - Window_Start[k]) /\ s <= (t - Window_End))
  (PickScheduling[s,k,t] + OverPickScheduling[s,k,t]) >= Demand[t,k]
);



/* 4. Limit part-time shifts */
% â(f in F|L(f)â¤6) X(f) â¤  Ratio_Part_to_Full*â(f in F|L(f)>6)X(f)    

constraint
sum(f in ShiftSet where Shift_Len[f] > Hour_Part)
  (Shift_Assign[f]) >= 
  Ratio_Full_to_Part*sum(f in ShiftSet where Shift_Len[f] <= Hour_Part)
  (Shift_Assign[f]);


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%            Solve and Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

solve minimize objective;
output ["Objective: "++show(objective) ++ "\n" ++
        "number of pickers required by each shift:" ++ "\n" ++ 
        show(Shift_Assign) ];

output [ "\n" ++
        "\(f) = \(Shift_Assign[f]);" | f in ShiftSet];


% output [ objective];
% output [ "\(p) = \(produce[p]);\n" | p in Products ]
